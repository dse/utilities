#!/usr/bin/env perl
use warnings;
use strict;
use Getopt::Long;

our $opt_trim;

Getopt::Long::Configure("gnu_getopt");
Getopt::Long::GetOptions(
    "t|trim" => \$opt_trim,
) or die(":-(\n");

my $lastFilename;
while (<>) {
    s{\R\z}{};                  # safer chomp
    processLine($_);
}

sub processLine {
    my ($line) = @_;
    local $_ = $line;
    my $filename;
    my $lineNumber;
    if (m{^Binary file .* matches}i) {
        $lastFilename = undef;
        print("$_\n");
        return;
    }
    if (s{^(?!\d+:)(?<filename>.*?):}{}x) {
        $filename = $+{filename};
        if ((!defined $lastFilename) || ($lastFilename ne $filename)) {
            printf("%s\n", $filename);
            $lastFilename = $filename;
        }
    }
    if (s{^\s*(?<lineNumber>\d+):}{}x) {
        $lineNumber = $+{lineNumber};
    }
    print('    ');
    printf("%6d:", $lineNumber) if defined $lineNumber;
    if ($opt_trim) {
        s{^\s+}{};
        s{\s+$}{};
    }
    print("$_\n");
}

=head1 NAME

C<grepfmt>, the grep output beautifier

=head1 SYNOPSIS

grep <arguments> | grepfmt

=head1 DESCRIPTION

C<grepfmt> beautifies grep's output by printing each filename once and
indenting each matching line.

C<grepfmt> handles the output of grep when it is invoked with most
options.  See L</CAVEATS>.

=head1 EXAMPLE

    $ grep verisi /usr/share/dict/* | grepfmt
    /usr/share/dict/web2
        inverisimilitude
        verisimilar
        verisimilarly
        verisimilitude
        verisimilitudinous
        verisimility
    /usr/share/dict/words
        inverisimilitude
        verisimilar
        verisimilarly
        verisimilitude
        verisimilitudinous
        verisimility

=head1 CAVEATS

If grep is invoked with both C<--line-number> and C<--byte-offset>,
C<grepfmt> will treat the byte offset as part of the matching line.

C<grepfmt> does not yet support grep's output if it is invoked with
any of the context options: C<-A>, C<--after-context>, C<-B>,
C<--before-context>, C<-C>, C<--context>.  I may remedy this in the
future.

C<grepfmt>'s output is undefined if grep is invoked with any of the
list of options below.  In these cases, C<grepfmt> has no intended
purpose or forseen utility.

=over 4

=item *

-c, --count

=item *

-L, --files-without-match

=item *

-l, --files-with-match

=item *

--null

=item *

-q, --quiet, --silent

=item *

-V, --version

=back

=head1 BUGS

None of which your author is aware.

=head1 AUTHOR

Darren Embry (C<dse@webonastick.com>)

=head1 LICENSE

You can use and distribute this program under the same terms as Perl
itself.
