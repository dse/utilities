#!/usr/bin/env perl
use warnings;
use strict;
use v5.10.0;

use Time::ParseDate qw(parsedate);
use POSIX qw(round strftime);

my $firstarg = $ARGV[0];
if (defined $firstarg &&
        ($firstarg eq "since" ||
         $firstarg eq "until" ||
         $firstarg eq "beween")) {
    shift @ARGV;
}

if (!scalar @ARGV || scalar @ARGV > 2) {
    usage();
    exit 1;
}

# needed in all cases, in case relative dates are specified
our $now = parsedate("midnight");

my @date = @ARGV;
my @time_t;
foreach my $date (@date) {
    my $time_t = parsedate($date, NOW => $now);
    if (!defined $time_t) {
        die("bad date: $date\n");
    }
    push(@time_t, $time_t);
}

our $date_format = "%a %Y-%m-%d";

if (scalar @date == 1) {
    since_or_until($time_t[0]);
} elsif (scalar @date == 2) {
    between($time_t[0], $time_t[1]);
}

sub since_or_until {
    my ($then) = @_;
    my $days_since = round(($now - $then) / 86400);
    my $then_fmt = strftime($date_format, localtime($then));
    if (-t 1) {
        if ($days_since >= 0) {
            printf("%d days since %s\n", $days_since, $then_fmt);
        } else {
            printf("%d days until %s\n", -$days_since, $then_fmt);
        }
    } else {
        say $days_since;
    }
}

sub between {
    my ($before, $after) = @_;
    my $days = round(($after - $before) / 86400);
    my $before_fmt = strftime($date_format, localtime($before));
    my $after_fmt  = strftime($date_format, localtime($after));
    if (-t 1) {
        if ($days >= 0) {
            printf("%d days from %s to %s\n", $days, $before_fmt, $after_fmt);
        } else {
            printf("%d days from %s to %s\n", -$days, $after_fmt, $before_fmt);
        }
    } else {
        say $days;
    }
}

sub usage { print <<"EOF"; }
usage:
    days [since|until] '<date>'
    days [between] '<date>' '<date>'
<date>:
    recommended unambiguous format is: '2015-01-31'
    for other formats: perldoc Time::ParseDate
examples:
    days '2015-01-01'                 days since '2015-01-01'
    days '2055-12-31'                 days until '2055-12-31'
    days '2015-01-01' '2055-12-31'    days between '2015-01-01' '2055-12-31'
EOF
