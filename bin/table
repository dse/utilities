#!/usr/bin/env perl
use utf8;
use warnings;
use strict;
use v5.10.0;

use open IO => ":locale";

our $opt_tabs;
our $opt_excel_header;
our $opt_unicode;
our $opt_borders;
our $opt_row_borders;
our $ifs;
our $ofs;

sub usage { print <<END; }
usage:
    table [<option> ...] [<file> ...]
options:
    -t, --tabs, --tab-separated-values, --tsv
    -x, --excel-header
    -u, --unicode
    -b, --borders
    -r, --row-borders
        --ifs=<string>
        --ofs=<string>
        --ifs-regexp=<string>
    -h, --help
END

use Getopt::Long;
Getopt::Long::Configure("bundling", "gnu_compat");
Getopt::Long::GetOptions(
    "t|tabs|tab-separated-values|tsv" => \$opt_tabs,
    "x|excel-header" => \$opt_excel_header,
    "u|unicode"      => \$opt_unicode,
    "b|borders"      => \$opt_borders,
    "r|row-borders"  => \$opt_row_borders,
    "ifs|input-field-separator=s"  => \$ifs,
    "ofs|output-field-separator=s" => \$ofs,
    "ifs-regexp|input-field-separator-regexp=s" => sub {
        my $arg = $_[1];
        $ifs = qr{$arg};
    },
    "h|help" => sub { usage(); exit(0); }
) or die("Type '$0 --help' for help.\n");

use lib "$ENV{HOME}/git/dse.d/utilities/lib";
use My::Text::Table;

my $t = My::Text::Table->new();
$t->hasExcelHeader($opt_excel_header) if defined $opt_excel_header;
$t->useUnicode($opt_unicode)          if defined $opt_unicode;
$t->hasVerticalBorders(1)             if $opt_borders;
$t->hasRowBorders(1)                  if $opt_row_borders;
$t->inputFieldSeparator($ifs)         if defined $ifs;
$t->outputFieldSeparator($ofs)        if defined $ofs;

while (<>) {
    s{\R\z}{};
    if (defined $ifs) {
        $t->addRow(split($ifs));
    } elsif ($opt_tabs) {
        $t->addRow(split("\t"));
    } else {
        $t->addRow(split);
    }
}
print $t->asString;
