#!/usr/bin/perl
use warnings;
use strict;
use File::Basename qw(dirname);
use Getopt::Long;

our $verbose = 0;
our $indent_string = "\t";

Getopt::Long::Configure(qw(bundling gnu_compat));
Getopt::Long::GetOptions("v|verbose" => \$verbose,
			 "h|help" => sub { usage(); exit(0); }) ||
	die("Type '$0 --help' for help.\n");

cat_apache2_conf(@ARGV);

my $dirname;
my $indent_level = 0;

sub cat_apache2_conf {
	my @files = @_;
	if (!scalar(@files)) { @files = ("-"); }
	local $_;
	foreach my $file (@files) {
		my $fh;
		if (!open($fh, "<", $file)) {
			warn("$0: $file: not found");
			next;
		}
		if (!defined $dirname) {
			$dirname = dirname($file);
		}
		print("### BEGIN $file ###\n") if $verbose;
		while (<$fh>) {
			chomp();
			next if /^\s*\#/;
			next unless /\S/;
			my $print = 1;
			s/^\s*//;
			if (m{^serverroot\s*(\"[^\"]*\"|\S+)}i) {
				$dirname = $1;
			} elsif (m{^include\s*(\"[^\"]*\"|\S+)}i) {
				my $glob = "$dirname/$1";
				if (-d $glob) {
					$glob = "$glob/*";
				}
				my @glob = glob($glob);
				print("### $_ => glob($glob) => @glob\n") if $verbose;
				cat_apache2_conf(@glob);
				$print = 0;
			} 
			if (/^\s*<\s*\//) {
				if ($indent_level >= 1) {
					$indent_level -= 1;
				}
			}
			print(($indent_string x $indent_level), $_, "\n") if $print;
			if (/^\s*<(?!\s*\/)/) {
				$indent_level += 1;
			}
		}
		print("### END $file ###\n") if $verbose;
	}
}

