#!/usr/bin/perl
use warnings;
use strict;
use File::Basename qw(dirname);
use Getopt::Long;
use File::Spec::Functions qw(rel2abs);

our $verbose = 0;
our $indent_string = "\t";

Getopt::Long::Configure(qw(bundling gnu_compat));
Getopt::Long::GetOptions("v|verbose" => \$verbose,
			 "h|help" => sub { usage(); exit(0); }) ||
	die("Type '$0 --help' for help.\n");

my $serverroot;
my $indent_level = 0;

cat_apache2_conf(@ARGV);

sub cat_apache2_conf {
	my @files = @_;
	if (!scalar(@files)) { @files = ("-"); }
	local $_;
	foreach my $file (@files) {
		my $fh;
		if (!open($fh, "<", $file)) {
			warn("$0: $file: not found");
			next;
		}
		if (!defined $serverroot) {
			$serverroot = dirname($file);
		}
		print("### BEGIN $file ###\n") if $verbose;
		while (<$fh>) {
			chomp();
			next if m{^\s*\#};
			next unless m{\S};
			my $print = 1;
			s{^\s*}{};
			if (m{^serverroot\s*(?:\"([^\"]*)\"|(\S+))}i) {
				$serverroot = $1;
			} elsif (m{^include\s*(?:\"([^\"]*)\"|(\S+))}i) {
				my $path = $1 // $2;
				my $glob = rel2abs($path, $serverroot);
				if (-d $glob || $glob =~ m{/$}) {
					$glob = "$glob/*";
				}
				my @glob = glob($glob);
				print("### $_ => glob($glob) => @glob\n") if $verbose;
				cat_apache2_conf(@glob);
				$print = 0;
			} 
			if (m{^\s*<\s*/}) {
				if ($indent_level >= 1) {
					$indent_level -= 1;
				}
			}
			print(($indent_string x $indent_level), $_, "\n") if $print;
			if (m{^\s*<(?!\s*/)}) {
				$indent_level += 1;
			}
		}
		print("### END $file ###\n") if $verbose;
	}
}

