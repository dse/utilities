#!/usr/bin/perl
use warnings;
use strict;
use File::Basename qw(dirname);
use Getopt::Long;
use File::Spec::Functions qw(rel2abs);

our $verbose = 0;
our $indent_string = "\t";
our $reorder = 0;
our $line_numbers = 0;

Getopt::Long::Configure(qw(bundling gnu_compat));
Getopt::Long::GetOptions("v|verbose+"      => \$verbose,
			 "reorder+"        => \$reorder,
			 "n|line-numbers+" => \$line_numbers,
			 "h|help" => sub { usage(); exit(0); }) ||
	die("Type '$0 --help' for help.\n");

my $serverroot;
my $indent_level = 0;
my $line_number = 0;

cat_apache2_conf(@ARGV);

my @primary;
my @secondary;

sub cat_apache2_conf {
	my @files = @_;
	if (!scalar(@files)) { @files = ("-"); }
	local $_;
	foreach my $file (@files) {
		my $fh;
		if (!open($fh, "<", $file)) {
			warn("$0: $file: not found");
			next;
		}
		if (!defined $serverroot) {
			$serverroot = dirname($file);
		}
		print("### BEGIN $file ###\n") if $verbose;
		while (<$fh>) {
			++$line_number;
			chomp();
			next if m{^\s*\#};
			next unless m{\S};
			my $print = 1;
			s{^\s*}{};
			my $list = \@primary;
			my $command = (m{^\s*(\S+)});
			if (m{^serverroot\s*(?:\"([^\"]*)\"|(\S+))}i) {
				$serverroot = $1;
			} elsif (m{^include\s*(?:\"([^\"]*)\"|(\S+))}i) {
				my $path = $1 // $2;
				my $glob = rel2abs($path, $serverroot);
				if (-d $glob || $glob =~ m{/$}) {
					$glob = "$glob/*";
				}
				my @glob = glob($glob);
				print("### $_ => glob($glob) => @glob\n") if $verbose;
				cat_apache2_conf(@glob);
				$print = 0;
			} 
			if (m{^\s*<\s*/}) {
				$list = \@secondary;
				if ($indent_level >= 1) {
					$indent_level -= 1;
				}
			}
			my $out = ($indent_string x $indent_level) . $_;
			if ($line_numbers) {
				$out = $line_number . "\t" . $out;
			}
			$list = \@secondary if $indent_level;
			if (m{^\s*<(?!\s*/)}) {
				$list = \@secondary;
				$indent_level += 1;
			}

			if ($print) {
				if ($reorder) {
					push(@$list, { line => $out,
						       line_number => $line_number,
						       command => $command });
				} else {
					print($out, "\n");
				}
			}
		}
		print("### END $file ###\n") if $verbose;
	}
}

if ($reorder) {
	print($_->{line} . "\n") for @primary;
	print($_->{line} . "\n") for @secondary;
}
