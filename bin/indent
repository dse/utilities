#!/usr/bin/env perl
use warnings;
use strict;

use POSIX qw(dup2);

my ($read1, $write1);
if (!pipe($read1, $write1)) {
    die("pipe: $!");
}
my $pid1 = fork();
if (!defined $pid1) {
    die("fork: $!");
}
if (!$pid1) {
    close($write1);
    dup2(fileno($read1), fileno(\*STDIN)); # redirect stdin from the first pipe
    exec('sed', 's/^/<OUT>   /');
}
close($read1);

my ($read2, $write2);
if (!pipe($read2, $write2)) {
    die("pipe: $!");
}
my $pid2 = fork();
if (!defined $pid2) {
    die("fork: $!");
}
if (!$pid2) {
    close($write2);
    dup2(fileno($read2), fileno(\*STDIN)); # redirect stdin from the second pipe
    dup2(fileno(\*STDERR), fileno(\*STDOUT)); # redirect stdout to stderr
    exec('sed', 's/^/<ERR>   /');
}
close($read2);

dup2(fileno($write1), fileno(\*STDOUT));  # redirect stdout to the first pipe
dup2(fileno($write2), fileno(\*STDERR));  # redirect stderr to the second pipe
exec(@ARGV);
