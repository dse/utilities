#!/usr/bin/env perl
use warnings;
use strict;
use open qw(locale);

use List::Util qw(max);
use Text::Tabs qw(expand);
use Getopt::Long qw();
use Data::Dumper qw(Dumper);

our $flagsAllPatterns = {};
our $flagsThisPattern = {};

our @patternInfo;

$Data::Dumper::Sortkeys = 1;

Getopt::Long::Configure(qw(gnu_compat bundling no_getopt_compat no_ignore_case));
Getopt::Long::GetOptions(
    # -l, --last-match, etc. turn the option on.
    # -L, --no-last-match, etc. turn the option off
    'l|last-match!'        => sub { my $hash = scalar(@patternInfo) ? $flagsThisPattern : $flagsAllPatterns; $hash->{last}   = $_[1]; },
    'b|before=s'           => sub { my $hash = scalar(@patternInfo) ? $flagsThisPattern : $flagsAllPatterns; $hash->{before} = $_[1]; },
    'r|right|align-right!' => sub { my $hash = scalar(@patternInfo) ? $flagsThisPattern : $flagsAllPatterns; $hash->{right}  = $_[1]; },
    'L'                    => sub { my $hash = scalar(@patternInfo) ? $flagsThisPattern : $flagsAllPatterns; $hash->{last}   = 0; },
    'B'                    => sub { my $hash = scalar(@patternInfo) ? $flagsThisPattern : $flagsAllPatterns; $hash->{before} = 0; },
    'R'                    => sub { my $hash = scalar(@patternInfo) ? $flagsThisPattern : $flagsAllPatterns; $hash->{right}  = 0; },
    'e|pattern=s' => sub {
        # specify above flags after each pattern to affect only that pattern
        # specify above flags before first pattern to affect all patterns
        $flagsThisPattern = {};
        push(@patternInfo, { pattern => $_[1], flags => $flagsThisPattern });
    }
) or die("exiting");

foreach my $patternInfo (@patternInfo) {
    %{$patternInfo->{flags}} = (%$flagsAllPatterns, %{$patternInfo->{flags}});
}

if (!scalar @patternInfo) {
    my $pattern = shift(@ARGV);
    if (!defined $pattern) {
        die("required pattern argument not found\n");
    }
    push(@patternInfo, { pattern => $pattern, flags => $flagsAllPatterns });
    $flagsThisPattern = {};
}

my @lines;
my $oldARGV;
while (<>) {
    if (!defined $oldARGV || $oldARGV ne $ARGV) {
        printLines();
        @lines = ();
        $oldARGV = $ARGV;
    }
    s{\R\z}{};                  # safer chomp
    s{^\s+}{};                  # trim beginning of string
    push(@lines, expand($_));
}
printLines();

sub printLines {
    return unless scalar @lines;
    my @lineData;
  line:
    foreach (@lines) {          # $_ local implicit
        my $origLine = $_;
        my @matches;
        my $str = $_;
      pattern:
        foreach my $patternInfo (@patternInfo) {

            my $stringToMatch = $str;
            my %flags = %{$patternInfo->{flags}};

            if ($flags{last} && defined $flags{before}) {
                my $pos = index($str, $flags{before});
                if ($pos > -1) {
                    $stringToMatch = substr($stringToMatch, 0, $pos);
                }
            }
            my $pattern = $patternInfo->{pattern};
            my $pos;
            if ($flags{last}) {
                $pos = rindex($str, $pattern);
            } else {
                $pos = index($str, $pattern);
            }
            if ($pos < 0 || $pos > length($str) - 1) {
                my $match = { str => $str };
                push(@matches, $match);
                push(@lineData, { text => $origLine, matches => \@matches });
                next line;
            }
            my $substr = substr($str, 0, $pos);
            my $match = { str => substr($str, 0, $pos), pos => $pos, pattern => $patternInfo };
            push(@matches, $match);
            $str = substr($str, $pos + length($pattern));
        }
        if (length $str) {
            my $match = { str => $str };
            push(@matches, $match);
        }
        push(@lineData, { text => $origLine, matches => \@matches });
    }

  line2:
    foreach my $patternIdx (0 .. $#patternInfo) {
        my $patternInfo = $patternInfo[$patternIdx];
        my $pattern = $patternInfo->{pattern};

        # lines that had this pattern match
        my @matches = grep { defined $_ } map {
            my $matches = $_->{matches}; # each line's matches
            my $hasMatch = $patternIdx <= $#$matches;
            my $match = $hasMatch ? $matches->[$patternIdx] : undef;
            my $matchesPattern = defined $match && defined $match->{pos};
            $matchesPattern ? $match : undef;
        } @lineData;

        if (!scalar @matches) {
            last line2;
        }
        my $textColumns    = max(grep { defined $_ } map { length($_->{str})                } @matches);
        my $patternColumns = max(grep { defined $_ } map { length($_->{pattern}->{pattern}) } @matches);
        foreach my $match (@matches) {
            if (defined $match->{pos}) {
                $match->{textColumns}    = $textColumns    if defined $textColumns;
                $match->{patternColumns} = $patternColumns if defined $patternColumns;
            }
        }
    }

  line3:
    foreach my $lineData (@lineData) {
      match3:
        foreach my $i (0 .. $#{$lineData->{matches}}) {
            my $match = $lineData->{matches}->[$i];
            if (!defined $match->{pos}) {
                printf("%s\n", $match->{str});
                next line3;
            }
            my %flags = %{$match->{pattern}->{flags}};
            my $format = $flags{right} ? "%*s %-*s " : "%-*s %-*s ";
            printf($format,
                   $match->{textColumns},
                   $match->{str},
                   $match->{patternColumns},
                   $match->{pattern}->{pattern});
        }
        print("\n");
    }
}
