#!/usr/bin/env perl
use warnings;
use strict;
use utf8;
use feature 'say';

#------------------------------------------------------------------------------
use File::Basename qw(basename);
our $PROGNAME = basename($0);

#------------------------------------------------------------------------------
use open IO => qw(:locale);
use Encode::Locale;
use Encode qw(decode);
BEGIN {
    @ARGV = map { decode('locale', $_, Encode::FB_CROAK) } @ARGV;
}

###############################################################################

while (<>) {
    s{\R\z}{};
    if ($PROGNAME eq 'cescape') {
        say cEscape($_);
    } elsif ($PROGNAME eq 'phpescape') {
        say phpEscape($_, singleQuote => 1);
        say phpEscape($_);
    } elsif ($PROGNAME eq 'perlescape') {
        say perlEscape($_, singleQuote => 1);
        say perlEscape($_);
    } elsif ($PROGNAME eq 'jsonescape') {
        say jsonEscape($_);
    } elsif ($PROGNAME eq 'jsescape') {
        say jsEscape($_, singleQuote => 1);
        say jsEscape($_);
    } elsif ($PROGNAME eq 'javaescape') {
        say javaEscape($_);
    } elsif ($PROGNAME eq 'pythonescape') {
        say pythonEscape($_, singleQuote => 1);
        say pythonEscape($_);
    } elsif ($PROGNAME eq 'escape') {
        say 'C       '  . cEscape($_);
        say 'PHP\'    ' . phpEscape($_, singleQuote => 1);
        say 'PHP"    '  . phpEscape($_);
        say 'PERL\'   ' . perlEscape($_, singleQuote => 1);
        say 'PERL"   '  . perlEscape($_);
        say 'JSON    '  . jsonEscape($_);
        say 'JS\'     ' . jsEscape($_, singleQuote => 1);
        say 'JS"     '  . jsEscape($_);
        say 'JAVA    '  . javaEscape($_);
        say 'PYTHON\' ' . pythonEscape($_, singleQuote => 1);
        say 'PYTHON" '  . pythonEscape($_);
    }
}

sub phpEscape {
    my ($string, %options) = @_;
    if ($options{singleQuote}) {
        return phpEscapeSingleQuote($string);
    }
    return phpEscapeDoubleQuote($string);
}

sub phpEscapeSingleQuote {
    my ($string) = @_;
    $string =~ s{\\}{\\\\}g;
    $string =~ s{\'}{\\'}g;
    # `\` before anything except `\` or `'` is a literal `\`, so do
    # not escape anything else regardless of paranoia level.
    return "'" . $string . "'";
}

sub phpEscapeDoubleQuote {
    my ($string) = @_;
    $string =~ s{\\}{\\\\}g;
    $string =~ s{\$}{\\\$}g;
    $string =~ s{\"}{\\"}g;
    $string =~ s{\x09}{\\t}g;     # tab
    $string =~ s{\x0a}{\\n}g;     # line feed
    $string =~ s{\x0b}{\\v}g;     # vertical tab
    $string =~ s{\x0c}{\\f}g;     # form feed
    $string =~ s{\x0d}{\\r}g;     # carriage return
    $string =~ s{\x1b}{\\e}g;     # escape
    $string =~ s{[\x00-\x1f\x7f-\xff]}
                {"\\x" . sprintf('%02x', ord($&))}ge;
    $string =~ s{[\x{0100}-\x{10ffff}]}
                {"\\u{" . sprintf('%04x', ord($&)) . "}"}ge;
    return '"' . $string . '"';
}

sub cEscape {
    my ($string, %options) = @_;
    $string =~ s{\\}{\\\\}g;
    $string =~ s{\$}{\\\$}g;
    $string =~ s{\"}{\\"}g;
    $string =~ s{\'}{\\'}g;
    $string =~ s{\x07}{\\a}g;     # bell
    $string =~ s{\x08}{\\b}g;     # backspace
    $string =~ s{\x09}{\\t}g;     # tab
    $string =~ s{\x0a}{\\n}g;     # line feed
    $string =~ s{\x0b}{\\v}g;     # vertical tab
    $string =~ s{\x0c}{\\f}g;     # form feed
    $string =~ s{\x0d}{\\r}g;     # carriage return
    $string =~ s{[\x00-\x1f\x7f-\xff]}
                {"\\x" . sprintf('%02x', ord($&))}ge;
    $string =~ s{[\x{0100}-\x{ffff}]}
                {"\\u" . sprintf('%04x', ord($&))}ge;
    $string =~ s{[\x{10000}-\x{10ffff}]}
                {"\\U" . sprintf('%08x', ord($&))}ge;
    return '"' . $string . '"';
}

use Data::Dumper;

sub perlEscape {
    my ($string, %options) = @_;
    local $Data::Dumper::Indent = 0;
    local $Data::Dumper::Terse = 1;
    local $Data::Dumper::Useqq = $options{singleQuote} ? 1 : 0;
    return Data::Dumper::Dumper($string);
}

sub jsEscape {
    my ($string, %options) = @_;
    $string =~ s{\\}{\\\\}g;
    if ($options{singleQuote}) {
        $string =~ s{\'}{\\'}g;
    } else {
        $string =~ s{\"}{\\"}g;
    }
    if ($options{json}) {
        $string =~ s{/}{\\/}g;
    }
    $string =~ s{[\x00-\x1f\x7f-\x{ffff}]}
                {"\\u" . sprintf('%04x', ord($&))}ge;
    $string =~ s{[\x{10000}-\x{10ffff}]}
                {jsonEscapeSurrogatePair($&)}ge;
    if ($options{singleQuote}) {
        return "'" . $string . "'";
    }
    return '"' . $string . '"';
}

sub jsonEscape {
    my ($string, %options) = @_;
    return jsEscape($string, %options, json => 1);
}

sub jsonEscapeSurrogatePair {
    my ($char) = @_;
    my $codepoint = ord($char);
    return if $codepoint < 0x10000;
    return if $codepoint > 0x10ffff;
    my $highSurrogate = 0xd800 + int(($codepoint - 0x10000) / 1024);
    my $lowSurrogate  = 0xdc00 + ($codepoint - 0x10000) % 1024;
    return sprintf('\\u%04x\\u%04x', $highSurrogate, $lowSurrogate);
}

sub javaEscape {
    my ($string, %options) = @_;
    $string =~ s{\\}{\\\\}g;
    $string =~ s{\"}{\\"}g;
    $string =~ s{\x08}{\\b}g;   # backspace
    $string =~ s{\x09}{\\t}g;   # tab
    $string =~ s{\x0a}{\\n}g;   # line feed
    $string =~ s{\x0c}{\\f}g;   # form feed
    $string =~ s{\x0d}{\\r}g;   # carriage return
    $string =~ s{[\x00-\x1f\x7f-\x{ffff}]}
                {"\\u" . sprintf('%04x', ord($&))}ge;
    $string =~ s{[\x{10000}-\x{10ffff}]}
                {jsonEscapeSurrogatePair($&)}ge;
    return '"' . $string . '"';
}

sub pythonEscape {
    my ($string, %options) = @_;
    my $prefix = '';
    $string =~ s{\\}{\\\\}g;
    if ($options{singleQuote}) {
        $string =~ s{\'}{\\'}g;
    } else {
        $string =~ s{\"}{\\"}g;
    }
    $string =~ s{\x07}{\\a}g;     # bell
    $string =~ s{\x08}{\\b}g;     # backspace
    $string =~ s{\x09}{\\t}g;     # tab
    $string =~ s{\x0a}{\\n}g;     # line feed
    $string =~ s{\x0b}{\\v}g;     # vertical tab
    $string =~ s{\x0c}{\\f}g;     # form feed
    $string =~ s{\x0d}{\\r}g;     # carriage return
    $string =~ s{[\x00-\x1f\x7f-\xff]}
                {"\\x" . sprintf('%02x', ord($&))}ge;
    if ($string =~ m{[\x{0100}-\x{10ffff}]}) {
        $prefix = 'u';
    }
    $string =~ s{[\x{0100}-\x{ffff}]}
                {"\\u" . sprintf('%04x', ord($&))}ge;
    $string =~ s{[\x{10000}-\x{10ffff}]}
                {"\\U" . sprintf('%08x', ord($&))}ge;

    if ($options{singleQuote}) {
        return "'" . $string . "'";
    }
    return $prefix . '"' . $string . '"';
}
